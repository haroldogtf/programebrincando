Programa ::= Comando

Comando ::= Sequencia 
 		  | Declaracao
		  | Atribuicao
		  | Aplicacao
		  | If-Then-Else
		  | For
 		  | Acao 
		  	
Sequencia ::= Comando ";" Comando

Declaracao ::= DeclaracaoVariavel
			| DeclaracaoProcedimento
			| DeclaracaoJunta

DeclaracaoVariavel ::= "VAR" Id "=" Expressao

DeclaracaoProcedimento ::= "APRENDA" Id "(" [ListaParametros] ")" Comando "FIMAPRENDA"
ListaParametros ::= Id | Id "," ListaParametros

DeclaracaoJunta ::= "CRIA" Id "=" Id "JUNTA" Id

Atribuicao ::= Id "=" Expressao

Aplicacao ::= "DESENHE" Id"(" [ListaExpressao] ")"
ListaExpressao ::= Expressao | Expressao "," ListaExpressao

If-Then-Else ::= "SE" Expressao "FACA" Comando "SENAOFACA" Comando "FIMSE"

For ::= "REPETIR" Expressao "," Comando "FIMREPETIR"

Acao ::= "PARAFRENTE" Expressao
		| "PARATRAAS" Expressao
		| "GIRARDIREITA" Expressao
		| "GIRARESQUERDA" Expressao
		| "CORCANETA" Expressao
		| "USARCANETA" Expressao

Expressao ::= Valor
		  | ExpressaoBinaria	  
		  | Id

Valor ::= ValorConcreto
ValorConcreto ::= ValorInteiro
				| ValorBooleano
				| ValorCor

ValorInteiro  ::= [0-9]+
ValorBooleano ::= "VERDADEIRO" | "FALSO"
ValorCor      ::= "COR("expressao"," expressao"," expressao")"

ExpressaoBinaria ::= Expressao "+" Expressao
				   | Expressao "-" Expressao
				   | Expressao "==" Expressao
				   | Expressao ">"  Expressao  
				   | Expressao "<"  Expressao  
				   | Expressao ">=" Expressao  
				   | Expressao "<=" Expressao  

Id ::= [a-zA-Z]+